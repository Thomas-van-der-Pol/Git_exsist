SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[REPORT_INVOICE_LINE_SPECIFICATION]
	@FK_FINANCE_INVOICE INT
AS
BEGIN

	SET NOCOUNT ON;

	;WITH ALL_DATA AS (
		SELECT
			3 AS FLAG,
			FIL.FK_FINANCE_INVOICE_SECTION,
			FIL.FK_ASSORTMENT_PRODUCT,
			FIS.[DESCRIPTION] AS [SECTION_DESCRIPTION],
			FIL.QUANTITY,
			FIL.[DESCRIPTION],
			FIL.[DESCRIPTION_SPECIFICATION],
			FV.[PERCENTAGE],
			FIL.PRICE,
			FIL.PRICE_TOTAL,
			FIL.PRICE_INCVAT,
			FIL.PRICE_TOTAL_INCVAT
		FROM FINANCE_INVOICE_LINE FIL WITH (NOLOCK)
		LEFT JOIN FINANCE_VAT FV WITH (NOLOCK) ON FV.ID = FIL.FK_FINANCE_VAT
		LEFT JOIN FINANCE_INVOICE_SECTION FIS WITH (NOLOCK) ON FIS.ID = FIL.FK_FINANCE_INVOICE_SECTION
		WHERE FIL.FK_FINANCE_INVOICE = @FK_FINANCE_INVOICE
		  AND FIL.ACTIVE = 1
		  AND FIL.SHOW_ON_SPECIFICATION = 1
	), INCLUDE_SECTION AS (
		SELECT DISTINCT
			1 AS FLAG,
			AD.FK_FINANCE_INVOICE_SECTION,
			NULL AS FK_ASSORTMENT_PRODUCT,
            AD.SECTION_DESCRIPTION,
            NULL AS QUANTITY,
            AD.SECTION_DESCRIPTION AS [DESCRIPTION],
			NULL AS [DESCRIPTION_SPECIFICATION],
            NULL AS PERCENTAGE,
            NULL AS PRICE,
            NULL AS PRICE_TOTAL,
            NULL AS PRICE_INCVAT,
            NULL AS PRICE_TOTAL_INCVAT
		FROM ALL_DATA AD
		WHERE AD.FK_FINANCE_INVOICE_SECTION IS NOT NULL

		UNION ALL

		SELECT DISTINCT
			2 AS FLAG,
			AD.FK_FINANCE_INVOICE_SECTION,
			AD.FK_ASSORTMENT_PRODUCT AS FK_ASSORTMENT_PRODUCT,
            AD.SECTION_DESCRIPTION,
            NULL AS QUANTITY,
            AD.[DESCRIPTION] AS [DESCRIPTION],
			NULL AS [DESCRIPTION_SPECIFICATION],
            NULL AS PERCENTAGE,
            NULL AS PRICE,
            SUM(PRICE_TOTAL) AS PRICE_TOTAL,
            NULL AS PRICE_INCVAT,
            SUM(PRICE_TOTAL_INCVAT) AS PRICE_TOTAL_INCVAT
		FROM ALL_DATA AD
		WHERE AD.FLAG = 3
		GROUP BY AD.FK_FINANCE_INVOICE_SECTION, AD.FK_ASSORTMENT_PRODUCT, AD.SECTION_DESCRIPTION, AD.[DESCRIPTION]

		UNION ALL

		SELECT
			AD.FLAG,
			AD.FK_FINANCE_INVOICE_SECTION,
			AD.FK_ASSORTMENT_PRODUCT,
            AD.SECTION_DESCRIPTION,
            AD.QUANTITY,
            AD.[DESCRIPTION],
			AD.DESCRIPTION_SPECIFICATION,
            AD.[PERCENTAGE],
            AD.PRICE,
            AD.PRICE_TOTAL,
            AD.PRICE_INCVAT,
            AD.PRICE_TOTAL_INCVAT
		FROM ALL_DATA AD
	)

	SELECT 
		* 
	FROM INCLUDE_SECTION
	ORDER BY [FK_FINANCE_INVOICE_SECTION], [FK_ASSORTMENT_PRODUCT], [FLAG], [DESCRIPTION]

END
GO
