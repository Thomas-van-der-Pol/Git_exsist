SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO

CREATE PROCEDURE [dbo].[FINANCE_ACCOUNTANCY_DEBTOR]
	@FK_CORE_DROPDOWNVALUE_ADRESSTYPE_INVOICE INT,
	@FK_CORE_DROPDOWNVALUE_ADRESSTYPE_VISIT INT,
	@FILTER NVARCHAR(MAX) = '',
	@IDs NVARCHAR(MAX) = ''
AS
BEGIN

	SET NOCOUNT ON;
	
	SELECT
		CR.ID,
		CR.NUMBER_DEBTOR AS DEBTORNUMBER,
		NULL AS [PAYMENT_CONDITION], -- Aantal dagen

		CR.NAME,
		CR.PHONENUMBER,
		CR.EMAILADDRESS,
		CR.VAT_LIABLE,
		CR.VAT_NUMBER,
		NULL AS STARTDATE,
		CR.REMARKS,
		ISNULL(NULLIF(CR.EXACT_DEBTOR_ERROR, '') + ' ', '') + ISNULL(NULLIF([ADDRESS_INVOICE].EXACT_ADDRESS_ERROR, '') + ' ', '') + ISNULL(NULLIF([ADDRESS_VISIT].EXACT_ADDRESS_ERROR, '') + ' ', '') AS EXACT_DEBTOR_ERROR,
		
		[ADDRESS_INVOICE].RELATION_ADDRESS_ID AS [INVOICE_RELATION_ADDRESS_ID],
		[ADDRESS_INVOICE].ID AS [INVOICE_ADDRESS_ID],
		[ADDRESS_INVOICE].ADDRESSLINE AS [INVOICE_ADDRESS_ADDRESSLINE],
		[ADDRESS_INVOICE].HOUSENUMBER AS [INVOICE_ADDRESS_HOUSENUMBER],
		[ADDRESS_INVOICE].ZIPCODE AS [INVOICE_ADDRESS_ZIPCODE],
		[ADDRESS_INVOICE].CITY AS [INVOICE_ADDRESS_CITY],
		[COUNTRY_INVOICE].COUNTRYCODE AS [INVOICE_ADDRESS_COUNTRYCODE],
		[ADDRESS_INVOICE].EXACT_ADDRESS_ERROR AS [INVOICE_ADDRESS_ERROR],

		[ADDRESS_VISIT].RELATION_ADDRESS_ID AS [VISIT_RELATION_ADDRESS_ID],
		[ADDRESS_VISIT].ID AS [VISIT_ADDRESS_ID],
		[ADDRESS_VISIT].ADDRESSLINE AS [VISIT_ADDRESS_ADDRESSLINE],
		[ADDRESS_VISIT].HOUSENUMBER AS [VISIT_ADDRESS_HOUSENUMBER],
		[ADDRESS_VISIT].ZIPCODE AS [VISIT_ADDRESS_ZIPCODE],
		[ADDRESS_VISIT].CITY AS [VISIT_ADDRESS_CITY],
		[COUNTRY_VISIT].COUNTRYCODE AS [VISIT_ADDRESS_COUNTRYCODE],
		[ADDRESS_VISIT].EXACT_ADDRESS_ERROR AS [VISIT_ADDRESS_ERROR]
	FROM CRM_RELATION CR WITH (NOLOCK)
	OUTER APPLY (
		SELECT TOP 1
			CA.*,
			CRA.ID AS RELATION_ADDRESS_ID,
			CRA.EXACT_ADDRESS_LASTSYNC,
			CRA.EXACT_ADDRESS_ERROR
		FROM CRM_RELATION_ADDRESS CRA WITH (NOLOCK)
		JOIN CORE_ADDRESS CA WITH (NOLOCK) ON CA.ID = CRA.FK_CORE_ADDRESS
		WHERE CRA.FK_CRM_RELATION = CR.ID
		  AND CRA.FK_CORE_DROPDOWNVALUE_ADRESSTYPE = @FK_CORE_DROPDOWNVALUE_ADRESSTYPE_INVOICE
		  AND CRA.ACTIVE = 1
	) [ADDRESS_INVOICE]
	LEFT JOIN CORE_COUNTRY [COUNTRY_INVOICE] WITH (NOLOCK) ON [COUNTRY_INVOICE].ID = [ADDRESS_INVOICE].FK_CORE_COUNTRY

	OUTER APPLY (
		SELECT TOP 1
			CA.*,
			CRA.ID AS RELATION_ADDRESS_ID,
			CRA.EXACT_ADDRESS_LASTSYNC,
			CRA.EXACT_ADDRESS_ERROR
		FROM CRM_RELATION_ADDRESS CRA WITH (NOLOCK)
		JOIN CORE_ADDRESS CA WITH (NOLOCK) ON CA.ID = CRA.FK_CORE_ADDRESS
		WHERE CRA.FK_CRM_RELATION = CR.ID
		  AND CRA.FK_CORE_DROPDOWNVALUE_ADRESSTYPE = @FK_CORE_DROPDOWNVALUE_ADRESSTYPE_VISIT
		  AND CRA.ACTIVE = 1
	) [ADDRESS_VISIT]
	LEFT JOIN CORE_COUNTRY [COUNTRY_VISIT] WITH (NOLOCK) ON [COUNTRY_VISIT].ID = [ADDRESS_VISIT].FK_CORE_COUNTRY
	
	WHERE CR.NUMBER_DEBTOR IS NOT NULL
	  AND CR.ACTIVE = 1
	  AND EXISTS (
		SELECT 1
		FROM FINANCE_INVOICE FI WITH (NOLOCK)
		WHERE FI.FK_CRM_RELATION = CR.ID
		  AND FI.FK_CORE_WORKFLOWSTATE = [dbo].[CORE_WORKFLOWSTATE_INVOICE_FINAL]()
		  AND FI.NUMBER IS NOT NULL
	  )
	  AND (
	    CR.EXACT_DEBTOR_LASTSYNC IS NULL
		OR
		(([ADDRESS_INVOICE].ID IS NOT NULL) AND ([ADDRESS_INVOICE].EXACT_ADDRESS_LASTSYNC IS NULL))
		OR
		(([ADDRESS_VISIT].ID IS NOT NULL) AND ([ADDRESS_VISIT].EXACT_ADDRESS_LASTSYNC IS NULL))
		OR
		CR.TS_LASTMODIFIED > CR.EXACT_DEBTOR_LASTSYNC
		OR
		(([ADDRESS_INVOICE].ID IS NOT NULL) AND ([ADDRESS_INVOICE].TS_LASTMODIFIED > [ADDRESS_INVOICE].EXACT_ADDRESS_LASTSYNC))
		OR
		(([ADDRESS_VISIT].ID IS NOT NULL) AND ([ADDRESS_VISIT].TS_LASTMODIFIED > [ADDRESS_VISIT].EXACT_ADDRESS_LASTSYNC))
		OR
		(
			EXISTS(
				SELECT 1 
				FROM FINANCE_INVOICE FI WITH (NOLOCK) 
				WHERE FI.FK_CRM_RELATION = CR.ID 
				  AND FI.FK_CORE_WORKFLOWSTATE = [dbo].[CORE_WORKFLOWSTATE_INVOICE_FINAL]()
				  AND FI.NUMBER IS NOT NULL
				  AND FI.EXACT_INVOICE_LASTSYNC IS NULL
			)
			AND
			CR.EXACT_DEBTOR_ID IS NULL
		)
	  )
	  AND (
		(((@FILTER IS NULL) OR (@FILTER = '')) OR CR.NUMBER_DEBTOR LIKE '%'+@FILTER+'%')
		OR
		(((@FILTER IS NULL) OR (@FILTER = '')) OR CR.NAME LIKE '%'+@FILTER+'%')
	  ) AND (
		((@IDs IS NULL) OR (@IDs = ''))
		OR
		CR.ID IN (SELECT ID FROM dbo.CORE_INLIST(@IDs))
	  )

END

GO
