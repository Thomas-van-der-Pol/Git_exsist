SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[FINANCE_BILLCHECK_CREATEINVOICES]
	@IDS NVARCHAR(MAX),
	@DATE DATE,
	@FK_CORE_DROPDOWNVALUE_ADRESSTYPE INT
AS
BEGIN

	SET NOCOUNT ON;

	UPDATE FINANCE_BILLCHECK SET 
	  DO_BILL = 0

	UPDATE FINANCE_BILLCHECK SET 
	  DO_BILL = 1
	WHERE ID IN (SELECT ID FROM [dbo].[CORE_INLIST](@IDS))
	  AND FK_CRM_CONTACT IS NOT NULL

	
	-- CREATE INVOICES
	INSERT INTO FINANCE_INVOICE (
		[FK_CRM_RELATION],
		[FK_CRM_RELATION_ADDRESS],
		[FK_CRM_CONTACT],
		[FK_CORE_LABEL],
		[FK_PROJECT],
		[FK_CORE_WORKFLOWSTATE],
		[IS_COLLECTIVE]
	)
	SELECT DISTINCT
		FB.FK_CRM_RELATION,
		COALESCE(FB.FK_CRM_RELATION_ADDRESS, [ADDRESS].[ID]),
		FB.FK_CRM_CONTACT,
		FB.FK_CORE_LABEL,
		FB.FK_PROJECT,
		[dbo].[CORE_WORKFLOWSTATE_INVOICE_CONCEPT](),
		[COLLECTION].[IS_COLLECTIVE]
	FROM FINANCE_BILLCHECK FB WITH (NOLOCK)
	OUTER APPLY (
		SELECT
			IIF(FB.FK_FINANCE_INVOICE_COLLECT_INTERVAL = [dbo].[FINANCE_INVOICE_COLLECT_INTERVAL_COLLECT_ALL](), 1, 0) AS [IS_COLLECTIVE]
	) [COLLECTION]
	OUTER APPLY (
		SELECT TOP 1
			CRA.ID
		FROM CRM_RELATION_ADDRESS CRA
		WHERE CRA.FK_CRM_RELATION = FB.FK_CRM_RELATION
		  AND CRA.FK_CORE_DROPDOWNVALUE_ADRESSTYPE = @FK_CORE_DROPDOWNVALUE_ADRESSTYPE
		  AND CRA.ACTIVE = 1
	) [ADDRESS]
	WHERE FB.ACTIVE = 1
	  AND FB.DO_BILL = 1
	  AND NOT EXISTS (
		SELECT 1
		FROM FINANCE_INVOICE FI WITH (NOLOCK)
		WHERE FI.FK_CORE_WORKFLOWSTATE = [dbo].[CORE_WORKFLOWSTATE_INVOICE_CONCEPT]()
		  AND FI.NUMBER IS NULL
		  AND (FI.[MANUAL] IS NULL OR FI.[MANUAL] = 0)
		  AND (FI.[IS_ADVANCE] IS NULL OR FI.[IS_ADVANCE] = 0)
		  AND FI.FK_CRM_RELATION = FB.FK_CRM_RELATION
		  AND ISNULL(FI.[FK_CRM_RELATION_ADDRESS], 0) = COALESCE(FB.FK_CRM_RELATION_ADDRESS, [ADDRESS].[ID], 0)
		  AND FI.FK_CRM_CONTACT = FB.FK_CRM_CONTACT
		  AND FI.FK_CORE_LABEL = FB.FK_CORE_LABEL
		  AND (
		    ((FB.FK_PROJECT > 0) AND (FI.FK_PROJECT = FB.FK_PROJECT))
			OR
			((FB.FK_PROJECT IS NULL) AND (FI.FK_PROJECT IS NULL))
		  )
		  AND FI.[IS_COLLECTIVE] = [COLLECTION].[IS_COLLECTIVE]
	  )

	-- SET INVOICE TARGETS
	UPDATE FB SET
	  FK_FINANCE_INVOICE_TARGET = FI.ID
	FROM FINANCE_BILLCHECK FB
	OUTER APPLY (
		SELECT
			IIF(FB.FK_FINANCE_INVOICE_COLLECT_INTERVAL = [dbo].[FINANCE_INVOICE_COLLECT_INTERVAL_COLLECT_ALL](), 1, 0) AS [IS_COLLECTIVE]
	) [COLLECTION]
	OUTER APPLY (
		SELECT TOP 1
			CRA.ID
		FROM CRM_RELATION_ADDRESS CRA
		WHERE CRA.FK_CRM_RELATION = FB.FK_CRM_RELATION
		  AND CRA.FK_CORE_DROPDOWNVALUE_ADRESSTYPE = @FK_CORE_DROPDOWNVALUE_ADRESSTYPE
		  AND CRA.ACTIVE = 1
	) [ADDRESS]
	JOIN FINANCE_INVOICE FI WITH (NOLOCK) ON FI.FK_CORE_WORKFLOWSTATE = [dbo].[CORE_WORKFLOWSTATE_INVOICE_CONCEPT]()
	  AND FI.NUMBER IS NULL
	  AND (FI.[MANUAL] IS NULL OR FI.[MANUAL] = 0)
	  AND (FI.[IS_ADVANCE] IS NULL OR FI.[IS_ADVANCE] = 0)
	  AND FI.FK_CRM_RELATION = FB.FK_CRM_RELATION
	  AND ISNULL(FI.[FK_CRM_RELATION_ADDRESS], 0) = COALESCE(FB.FK_CRM_RELATION_ADDRESS, [ADDRESS].[ID], 0)
	  AND FI.FK_CRM_CONTACT = FB.FK_CRM_CONTACT
	  AND FI.FK_CORE_LABEL = FB.FK_CORE_LABEL
	  AND (
		((FB.FK_PROJECT > 0) AND (FI.FK_PROJECT = FB.FK_PROJECT))
		OR
		((FB.FK_PROJECT IS NULL) AND (FI.FK_PROJECT IS NULL))
	  )
	  AND FI.[IS_COLLECTIVE] = [COLLECTION].[IS_COLLECTIVE]
	WHERE FB.ACTIVE = 1
	  AND FB.DO_BILL = 1


	-- INSERT INVOICE LINES
	INSERT INTO FINANCE_INVOICE_LINE (
		[FK_FINANCE_INVOICE],
		[FK_FINANCE_LEDGER],
		[FK_FINANCE_VAT],
		[FK_FINANCE_INVOICE_SCHEME],
		[FK_PROJECT],
		[FK_ASSORTMENT_PRODUCT],
		[QUANTITY],
		[QUANTITY_MONTH],
		[DESCRIPTION],
		[DESCRIPTION_SPECIFICATION],
		[SHOW_ON_SPECIFICATION],
		[PRICE],
		[STARTDATE],
		[ENDDATE],
		[IS_COMPENSATION]
	)
	SELECT
		FB.FK_FINANCE_INVOICE_TARGET,
		FB.FK_FINANCE_LEDGER,
		FB.FK_FINANCE_VAT,
		FB.FK_FINANCE_INVOICE_SCHEME,
		FB.FK_PROJECT,
		PAP.FK_ASSORTMENT_PRODUCT,
		FB.QUANTITY,
		FB.QUANTITY_MONTH,
		FB.[DESCRIPTION],
		FB.[DESCRIPTION_SPECIFICATION],
		FB.[SHOW_ON_SPECIFICATION],
		FB.PRICE,
		FB.STARTDATE,
		FB.ENDDATE,
		FB.IS_COMPENSATION
	FROM FINANCE_BILLCHECK FB WITH (NOLOCK)
	LEFT JOIN FINANCE_INVOICE_SCHEME FIS WITH (NOLOCK) ON FIS.ID = FB.FK_FINANCE_INVOICE_SCHEME
	LEFT JOIN PROJECT_ASSORTMENT_PRODUCT PAP WITH (NOLOCK) ON PAP.ID = FIS.FK_PROJECT_ASSORTMENT_PRODUCT
	WHERE FB.ACTIVE = 1
	  AND FB.DO_BILL = 1
	  AND FB.FK_FINANCE_INVOICE_TARGET IS NOT NULL


	-- UPDATE INVOICE SCHEMES
	UPDATE FIS SET
	  FK_FINANCE_INVOICE_LINE = FIL.ID
	FROM FINANCE_BILLCHECK FB
	INNER JOIN FINANCE_INVOICE_SCHEME FIS ON FIS.ID = FB.FK_FINANCE_INVOICE_SCHEME
	JOIN FINANCE_INVOICE_LINE FIL ON FIL.FK_FINANCE_INVOICE_SCHEME = FIS.ID
	WHERE FB.ACTIVE = 1
	  AND FB.DO_BILL = 1


	-- CALCULATE INVOICE SECTIONS
	DECLARE @FK_FINANCE_INVOICE INT

	DECLARE INVOICE_CREATE_CURSOR CURSOR FOR
		SELECT FK_FINANCE_INVOICE_TARGET
		FROM FINANCE_BILLCHECK FB WITH (NOLOCK)
		WHERE FB.ACTIVE = 1
		  AND FB.DO_BILL = 1;

	OPEN INVOICE_CREATE_CURSOR;

	FETCH NEXT FROM INVOICE_CREATE_CURSOR
	INTO @FK_FINANCE_INVOICE

	WHILE @@FETCH_STATUS = 0
	BEGIN

		--EXEC [FINANCE_INVOICE_RECALCULATE_SECTIONS] @FK_FINANCE_INVOICE
		EXEC [FINANCE_INVOICE_RECALCULATE_SPECIFICATION] @FK_FINANCE_INVOICE
		
		FETCH NEXT FROM INVOICE_CREATE_CURSOR
		INTO @FK_FINANCE_INVOICE

	END

	CLOSE INVOICE_CREATE_CURSOR;
	DEALLOCATE INVOICE_CREATE_CURSOR;


	-- RECALCULATE FINANCE PREPARE SCREEN
	EXEC dbo.FINANCE_BILLCHECK_FILL @DATE, @FK_CORE_DROPDOWNVALUE_ADRESSTYPE

END
GO
