SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[FINANCE_CREATE_ADVANCE_INVOICE]
	@FK_PROJECT INT,
	@TYPE INT,
	@PERCENTAGE DECIMAL(15, 6),
	@AMOUNT DECIMAL(15, 6),
	@FK_CORE_DROPDOWNVALUE_ADRESSTYPE INT
AS
BEGIN

	SET NOCOUNT ON;

	SET @PERCENTAGE = ISNULL(@PERCENTAGE, 0)
	SET @AMOUNT = ISNULL(@AMOUNT, 0)

	DECLARE @FK_FINANCE_INVOICE INT,
			@SUCCESS BIT = 0,
			@MAX_AMOUNT DECIMAL(15, 6) = 0,
			@CALCULATED_AMOUNT DECIMAL(15, 6) = 0

	SELECT
		@MAX_AMOUNT = ISNULL(P.FIXED_PRICE, 0)
	FROM PROJECT P WITH (NOLOCK)
	WHERE P.ID = @FK_PROJECT

	-- Percentage
	IF (@TYPE = 1)
	BEGIN
		IF (@PERCENTAGE <= 100)
		BEGIN
			SET @CALCULATED_AMOUNT = @MAX_AMOUNT * (@PERCENTAGE / 100)
		END
	END
	-- Fixed amount
	ELSE IF (@TYPE = 2)
	BEGIN
		IF (@AMOUNT <= @MAX_AMOUNT)
		BEGIN
			SET @CALCULATED_AMOUNT = @AMOUNT
		END
	END


	IF (@CALCULATED_AMOUNT > 0)
	BEGIN

		INSERT INTO FINANCE_INVOICE (
			[FK_CRM_RELATION],
			[FK_CRM_CONTACT],
			[FK_CORE_LABEL],
			[FK_PROJECT],
			[FK_CORE_WORKFLOWSTATE],
			[FK_CRM_RELATION_ADDRESS],
			[IS_ADVANCE]
		)
		SELECT TOP 1
			P.FK_CRM_RELATION,
			COALESCE(P.FK_CRM_CONTACT_INVOICE, P.FK_CRM_CONTACT),
			P.FK_CORE_LABEL,
			P.ID,
			[dbo].[CORE_WORKFLOWSTATE_INVOICE_CONCEPT](),
			[ADDRESS].[ID],
			CAST(1 AS BIT)
		FROM PROJECT P WITH (NOLOCK)
		OUTER APPLY (
			SELECT TOP 1
				CRA.ID
			FROM CRM_RELATION_ADDRESS CRA
			WHERE CRA.FK_CRM_RELATION = P.FK_CRM_RELATION
			  AND CRA.FK_CORE_DROPDOWNVALUE_ADRESSTYPE = @FK_CORE_DROPDOWNVALUE_ADRESSTYPE
			  AND CRA.ACTIVE = 1
		) [ADDRESS]
		WHERE P.ID = @FK_PROJECT

		SET @FK_FINANCE_INVOICE = SCOPE_IDENTITY()

		INSERT INTO FINANCE_INVOICE_LINE (
			[FK_FINANCE_INVOICE],
			[FK_FINANCE_LEDGER],
			[FK_FINANCE_VAT],
			[QUANTITY],
			[DESCRIPTION],
			[PRICE]
		)
		SELECT TOP 1
			@FK_FINANCE_INVOICE,
			NULL,
			NULL,
			1,
			'Voorschot op project ' + P.[DESCRIPTION],
			@CALCULATED_AMOUNT
		FROM PROJECT P WITH (NOLOCK)
		WHERE P.ID = @FK_PROJECT

		SET @SUCCESS = 1

	END

	SELECT 
		@SUCCESS AS SUCCESS,
		@FK_FINANCE_INVOICE AS ID

END
GO
